/*
 * ***************************************************************************
 *   Copyright 2014-2019 Spectra Logic Corporation. All Rights Reserved.
 *   Licensed under the Apache License, Version 2.0 (the "License"). You may not use
 *   this file except in compliance with the License. A copy of the License is located at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 *   or in the "license" file accompanying this file.
 *   This file is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
 *   CONDITIONS OF ANY KIND, either express or implied. See the License for the
 *   specific language governing permissions and limitations under the License.
 * ***************************************************************************
 */

plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10' apply false
    id 'org.owasp.dependencycheck' version '7.1.0.1'
}

allprojects {
    version = '5.1.4'
    group = 'com.spectralogic'

    repositories {
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'java'

    sourceCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'ch.qos.logback:logback-core:1.2.3'
        implementation 'ch.qos.logback:logback-classic:1.2.3'
        implementation 'com.google.guava:guava:23.0-jre'
        implementation 'com.github.spectralogic.ds3_java_sdk:ds3-sdk:5.1.2'
        implementation 'com.google.inject:guice:4.2.0'
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2'

        testImplementation('org.mockito:mockito-core:1.10.19') {
            exclude group: 'org.hamcrest'
        }

        testImplementation 'junit:junit:4.12'
        testImplementation 'org.hamcrest:hamcrest-library:1.3'
        testImplementation 'org.powermock:powermock-api-mockito:1.6.4'
        testImplementation 'org.powermock:powermock-module-junit4:1.6.4'
    }
}

project(':ds3-cli-helpers') {
    dependencies {
        implementation project(':ds3_java_cli')
    }
}

project(':ds3-cli-integration') {
    dependencies {
        testImplementation project(':ds3_java_cli')
        testImplementation project(':ds3-cli-helpers')
        testImplementation project(path: ':ds3-cli-helpers', configuration: 'testOutput')
    }
}

project(':ds3-cli-certification') {
    dependencies {
        implementation project(':ds3_java_cli')
        implementation project(':ds3-cli-helpers')
        testImplementation project(':ds3-cli-helpers')
        testImplementation project(path: ':ds3-cli-helpers', configuration: 'testOutput')
    }
}

wrapper {
    // to upgrade the gradle wrapper, bump the version below and run ./gradlew wrapper
    gradleVersion = '7.4.2'
}
