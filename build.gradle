/*
 * ***************************************************************************
 *   Copyright 2014-2019 Spectra Logic Corporation. All Rights Reserved.
 *   Licensed under the Apache License, Version 2.0 (the "License"). You may not use
 *   this file except in compliance with the License. A copy of the License is located at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 *   or in the "license" file accompanying this file.
 *   This file is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
 *   CONDITIONS OF ANY KIND, either express or implied. See the License for the
 *   specific language governing permissions and limitations under the License.
 * ***************************************************************************
 */

plugins {
    alias(libs.plugins.gradleDepVers)
    alias(libs.plugins.kotlinJvm).apply(false)
    alias(libs.plugins.owaspDepCheck)
}

allprojects {
    version = '5.1.4'
    group = 'com.spectralogic'

    repositories {
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'java'

    sourceCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation libs.logbackCore
        implementation libs.logbackClassic
        implementation libs.guava
        implementation libs.ds3Sdk
        implementation libs.guiceCore
        implementation libs.kotlinCoroutinesCore

        testImplementation(libs.mockitoCore) {
            exclude group: 'org.hamcrest'
        }

        testImplementation libs.junit
        testImplementation libs.hamcrestLib
        testImplementation libs.powermockMockito
        testImplementation libs.powermockJunit4
    }
}

project(':ds3-cli-helpers') {
    dependencies {
        implementation project(':ds3_java_cli')
    }
}

project(':ds3-cli-integration') {
    dependencies {
        testImplementation project(':ds3_java_cli')
        testImplementation project(':ds3-cli-helpers')
        testImplementation project(path: ':ds3-cli-helpers', configuration: 'testOutput')
    }
}

project(':ds3-cli-certification') {
    dependencies {
        implementation project(':ds3_java_cli')
        implementation project(':ds3-cli-helpers')
        testImplementation project(':ds3-cli-helpers')
        testImplementation project(path: ':ds3-cli-helpers', configuration: 'testOutput')
    }
}

wrapper {
    // to upgrade the gradle wrapper, bump the version below and run ./gradlew wrapper
    gradleVersion = '7.6'
}
